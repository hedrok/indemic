import os
at90usb_env = Environment(
    CC='avr-gcc',
    CXX='avr-g++',
    CPPPATH = ['../../..'],
    CXXFLAGS = '-std=c++11',
    CCFLAGS = '-Os -mmcu=at90usb162 -Wall',
    ENV = {'PATH' : os.environ['PATH']},
    LINKFLAGS = '-mmcu=at90usb162'
)
# We need only includes (not libs) of simavr for avr-mcu.c
at90usb_env.ParseConfig("pkg-config simavr --cflags")

avr_output_elf = at90usb_env.Program('avr-output.elf', ['avr-output.cpp', 'avr-mcu.c'])
avr_ddr_elf = at90usb_env.Program('avr-ddr.elf', ['avr-ddr.cpp', 'avr-mcu.c'])

cxxtest_env = Environment(
    tools = ['default','cxxtest'],
    CXXFLAGS = '-std=c++11',
)
cxxtest_env.ParseConfig("pkg-config simavr --cflags --libs")
test_iopin = cxxtest_env.CxxTest('test_iopin', ['IOPinTest.h'])
Depends(test_iopin, avr_output_elf)
Depends(test_iopin, avr_ddr_elf)
